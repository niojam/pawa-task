plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'
	id "com.github.davidmc24.gradle.plugin.avro" version "1.5.0"
}

group = 'ee.pawadeck'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

avro {
	outputCharacterEncoding = "UTF-8"
	stringType = "CharSequence"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url "https://packages.confluent.io/maven"
	}
}

dependencyManagement {
	imports {
		mavenBom 'org.springdoc:springdoc-openapi:2.2.0'
		mavenBom "org.testcontainers:testcontainers-bom:1.19.0"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.liquibase:liquibase-core'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.keycloak:keycloak-admin-client:22.0.0'

	implementation 'org.springframework.kafka:spring-kafka'

	implementation 'org.apache.avro:avro:1.11.2'
	implementation ('io.confluent:kafka-avro-serializer:7.5.1') {
		// swagger-annotations-jakarta have classes with the exact FQN but different contents
		exclude group: 'io.swagger.core.v3', module: 'swagger-annotations'
	}


	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'


	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j:8.2.0'

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	// lombok not taken from Spring boot BOM see issue https://stackoverflow.com/questions/77171270/compilation-error-after-upgrading-to-jdk-21-nosuchfielderror-jcimport-does-n
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mysql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}
